plugins {
  id "com.gradle.plugin-publish" version "0.12.0"
  id "com.michionlion.gitver" version "0.0.1"
  id "java-gradle-plugin"
  id "maven-publish"
  id "groovy"
}

group = "com.michionlion"

logger.lifecycle("\n\u001B[1;36mVERSION ${version}\u001B[0m\n")

setGradlePluginPortalAccessProperties()

pluginBundle {
  website = "https://github.com/michionlion/gradle-git-version"
  vcsUrl = "https://github.com/michionlion/gradle-git-version"
  description = "Automatically sets your version based on configurable git information"
  tags = ["git", "version", "gitver"]

  plugins {
    gitver {
      displayName = "GitVer"
    }
  }
}

gradlePlugin {
  plugins {
    gitver {
      id = "${group}.${name}"
      implementationClass = "com.michionlion.gitver.GitVersionPlugin"
    }
  }
}


def setGradlePluginPortalAccessProperties() {
  def key = System.getenv("GRADLE_PLUGIN_PORTAL_KEY")
  def secret = System.getenv("GRADLE_PLUGIN_PORTAL_SECRET")

  if (key) {
    System.properties.setProperty("gradle.publish.key", key)
  }
  if (secret) {
    System.properties.setProperty("gradle.publish.secret", secret)
  }
}
