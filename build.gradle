plugins {
  id "groovy"
  id "maven-publish"
  id "java-gradle-plugin"
  id "com.gradle.plugin-publish" version "0.13.0"
}

// load current source as plugin
def classpaths = [file("src/main/groovy").absolutePath, file("src/main/resources").absolutePath] as String[]
def engine = new GroovyScriptEngine(classpaths, this.getClass().getClassLoader())
def gitverPlugin = engine.loadScriptByName("com/wagner/gitver/GitVersionPlugin.groovy")
apply plugin: gitverPlugin

group = "com.wagner"

logger.lifecycle("\n\u001B[1;36mVERSION ${version}\u001B[0m\n")

setGradlePluginPortalAccessProperties()

pluginBundle {
  website = "https://github.com/michionlion/gradle-git-version"
  vcsUrl = "https://github.com/michionlion/gradle-git-version"
  tags = ["git", "version", "gitver"]
}

gradlePlugin {
  plugins {
    gitver {
      id = "${group}.${name}"
      displayName = "GitVer"
      description = "Automatically sets your version based on configurable git information"
      implementationClass = "com.wagner.gitver.GitVersionPlugin"
    }
  }
}


def setGradlePluginPortalAccessProperties() {
  def key = System.getenv("GRADLE_PLUGIN_PORTAL_KEY")
  def secret = System.getenv("GRADLE_PLUGIN_PORTAL_SECRET")

  if (key) {
    System.properties.setProperty("gradle.publish.key", key)
  }
  if (secret) {
    System.properties.setProperty("gradle.publish.secret", secret)
  }
}
