plugins {
  id "com.gradle.plugin-publish" version "1.2.0"
  id "groovy"
}

repositories {
  mavenCentral()
}

// load current source as plugin
def classpaths = [file("src/main/groovy").absolutePath, file("src/main/resources").absolutePath] as String[]
def engine = new GroovyScriptEngine(classpaths, this.getClass().getClassLoader())
def gitverPlugin = engine.loadScriptByName("com/wagner/gitver/GitVersionPlugin.groovy")
apply plugin: gitverPlugin

group = "com.wagner"

logger.lifecycle("\n\u001B[1;36mVERSION ${version}\u001B[0m\n")

gradlePlugin {
  website = "https://github.com/michionlion/gradle-git-version"
  vcsUrl = "https://github.com/michionlion/gradle-git-version"
  plugins {
    gitver {
      id = "${group}.${name}"
      implementationClass = "com.wagner.gitver.GitVersionPlugin"
      displayName = "GitVer"
      description = "Automatically sets your version based on configurable git information"
      tags.set(["git", "version", "gitver"])
    }
  }
}
